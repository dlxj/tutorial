Title: Contexts
Authors: 
Categories: mathematica-programming,code-structure
Date: 2017-12-29 02:55:31
ID: 2.1.6
Modified: 2018-07-19 00:17:22
Path: Mathematica Programming/Code Structure
Slug: contexts
Tags: contexts

<a id="contexts" style="width:0;height:0;margin:0;padding:0;">&zwnj;</a>

## Contexts

Contexts are a relatively simple concept but have major effects. Their basic use is to tag symbols with extra information. Usually we are in the global context:

    $Context

    (*Out:*)
    
    "Global`"

Although many of the functions we use are not:

    Context@Plot

    (*Out:*)
    
    "System`"

A context is a series of symbol names followed by  ```"`"```

Contexts we can use without fully qualifying the name are those on  ```$ContextPath```

    $ContextPath

    (*Out:*)
    
    {"CloudObjectLoader`","StreamingLoader`","SymbolicMachineLearningLoader`","IconizeLoader`","HTTPHandlingLoader`","PacletManager`","System`","Global`"}

We can define our own contexts via  ```Begin``` and  ```BeginPackage``` and the context is exited via  ```End``` and  ```EndPackage``` respectively. The only difference between these is that  ```BeginPackage``` restricts the value of  ```$ContextPath``` while  ```Begin``` does not:

    BeginPackage["NameTools`"];
    thisIsTheLocalContextPath=$ContextPath;
    EndPackage[]
    thisIsTheLocalContextPath//Context
    thisIsTheLocalContextPath

    (*Out:*)
    
    "NameTools`"

    (*Out:*)
    
    {"NameTools`","System`"}

This protects symbols from being affected by things in the  ```Global` ``` context or any other context.  ```Begin``` does not do this:

    Begin["NameTools`"];
    thisIsTheLocalContextPath=$ContextPath;
    End[];
    thisIsTheLocalContextPath

    (*Out:*)
    
    {"NameTools`","CloudObjectLoader`","StreamingLoader`","SymbolicMachineLearningLoader`","IconizeLoader`","HTTPHandlingLoader`","PacletManager`","System`","Global`"}

Note that  ```BeginPackage``` also prepended the context defined to the  ```$ContextPath``` .

Anything not in  ```NameTools` ``` exactly won't be found, however. We can see this by definining something in a subcontext:

    NameTools`hidden`a=100

    (*Out:*)
    
    100

And noting that it doesn't get exposed via context in the  ```$ContextPath``` :

    a
    a//Context

    (*Out:*)
    
    a

    (*Out:*)
    
    "Global`"

This is how Mathematica defines so many functions without us having to worry about using the wrong symbols. Just consider all of the symbols we can find in  ```System`Private` ``` :

    Pane[Column@Names@"System`Private`*",{Automatic,100},Scrollbars->{False,True},AppearanceElements->None]

    (*Out:*)
    
![216contexts-7468015508976701203]({filename}/img/216contexts-7468015508976701203.png)

One final useful thing to know is that the current context can be implied when defining a symbol, such as done in the following example:

    `Private`b=100

    (*Out:*)
    
    100

This just had the current context put in front of the first `

    Global`Private`b

    (*Out:*)
    
    100

<a id="see-also" style="width:0;height:0;margin:0;padding:0;">&zwnj;</a>

## See Also:

* [https://mathematica.stackexchange.com/questions/43381/how-symbol-lookup-actually-works/43629#43629](https://mathematica.stackexchange.com/questions/43381/how-symbol-lookup-actually-works/43629#43629)